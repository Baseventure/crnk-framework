import com.moowork.gradle.node.npm.NpmTask;

apply plugin: 'com.moowork.node'


def isProductionBuild = System.env.NEXUS_DEPLOY_USER != null
if(isProductionBuild){
	ext {
		NG_BUILD_ARGS = ['--environment', 'prod', '--target', 'production']
		NG_E2E_ARGS = NG_BUILD_ARGS
	}
}else{
	ext {
		NG_BUILD_ARGS = []
		NG_E2E_ARGS = NG_BUILD_ARGS
	}
}

node {
	version = '6.10.3'
	npmVersion = '5.0.3'
	download = true
	distBaseUrl = 'http://adn-nodejs-mirror.cust.adnovum.ch/dist'
}

def npmStagingDir = file("${buildDir}/npm")
task copyNpmFiles(type: Copy) {
	from(
		file('.npmrc'),
		file('.npmignore')
	)
	into npmStagingDir
}

npmInstall.inputs.file(file('package.json'))
npmInstall.outputs.dir(file('node_modules'))

task npmRunBuild(type: NpmTask) {
	dependsOn tasks.npmInstall
	dependsOn tasks.copyNpmFiles

	npmCommand = ['run', 'build']
	// TODO: Programmatically include all subfolders except buildDir?
	inputs.dir 'app'
	inputs.dir 'assets'
	inputs.dir 'binding'
	inputs.dir 'common'
	inputs.dir 'component'
	inputs.dir 'meta'
	inputs.dir 'presentation'

	inputs.files 'angular-cli.json', 'package.json'
	// We need this since the version is added to package.json during the build
	inputs.property 'version', project.version

	outputs.dir npmStagingDir

	// always do package.json in same task to have proper up-to-date checks
	// and properly updated version timestamps
	def packageInputFile = file("package.json")
	def PackageOutputFile = file("${npmStagingDir}/package.json")
	doLast {
		def packageJson = new groovy.json.JsonSlurper().parseText(packageInputFile.text)
		packageJson['version'] = project.version
		def jsonOutput = new groovy.json.JsonOutput()
		PackageOutputFile.createNewFile()
		PackageOutputFile.write jsonOutput.prettyPrint(jsonOutput.toJson(packageJson))
	}
}

task npmCacheVerify(type: NpmTask) {
	npmCommand = ['cache', 'verify']
	group = 'node'
}

task publishNpmPackage(type: NpmTask, dependsOn: [npmRunBuild]) {
	group = 'publishing'
	workingDir = npmStagingDir
	npmCommand = ['publish']
}

tasks.publish.dependsOn publishNpmPackage

tasks.assemble.dependsOn 'npmRunBuild'

clean.doFirst {
	delete "${projectDir}/node_modules/"
}
